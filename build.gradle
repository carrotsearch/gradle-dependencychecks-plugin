plugins {
  alias(libs.plugins.gradle.publish)
  alias(libs.plugins.spotless)
  id 'groovy'
  id 'signing'
}

def humanName = 'dependencychecks-plugin'
project.group = 'com.carrotsearch.gradle.dependencychecks'
project.version = libs.versions.projectVersion.get()
project.description = 'Dependency checker and lock file aggregator'

project.ext.websiteAddress = "https://github.com/carrotsearch/gradle-dependencychecks-plugin"
project.ext.vcsAddress = "https://github.com/carrotsearch/gradle-dependencychecks-plugin.git"

repositories {
  mavenCentral()
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(libs.versions.minJava.get())
  }
}

dependencies {
  implementation libs.jackson

  testImplementation libs.junit.jupiter
  testRuntimeOnly("org.junit.platform:junit-platform-launcher")

  testImplementation libs.assertj

  testImplementation platform(libs.spockframework.get())
  testImplementation "org.spockframework:spock-core"
}

gradlePlugin {
  website = project.ext.websiteAddress
  vcsUrl = project.ext.websiteAddress + ".git"
  plugins {
    dependencychecks {
      id = project.group
      implementationClass = 'com.carrotsearch.gradle.buildinfra.dependencychecks.DependencyChecksPlugin'
      displayName = project.description
      description = project.description
      tags = [
        'utility'
      ]
    }
  }
}

tasks.withType(Javadoc).configureEach {
  it.options.addBooleanOption("Xdoclint:none", true)
}

tasks.register("sourcesJar", Jar, {
  dependsOn classes
  archiveClassifier = 'sources'
  from sourceSets.main.allJava
})

tasks.register("javadocJar", Jar, {
  dependsOn javadoc
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
})

tasks.withType(Test).configureEach {
  useJUnitPlatform()
  systemProperty("tests.gradle.version", gradle.gradleVersion)
}

publishing {
  repositories {
    maven {
      name = 'staging'
      url = project.layout.buildDirectory.dir("m2-local")
    }

    maven {
      name = 'sonatype'
      url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"

      credentials {
        if (project.hasProperty('nexusUsername')) {
          username project.nexusUsername
        }
        if (project.hasProperty('nexusPassword')) {
          password project.nexusPassword
        }
      }
    }
  }

  publications {
    withType(MavenPublication).configureEach {
      pom {
        inceptionYear = "2023"

        name = project.name
        description = project.description
        url = websiteAddress

        licenses {
          license {
            name = 'Apache 2'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        organization {
          name = "Carrot Search s.c."
          url = "https://www.carrotsearch.com"
        }

        developers {
          developer {
            id = 'dawid.weiss'
            name = 'Dawid Weiss'
            email = 'dawid.weiss@carrotsearch.com'
          }
        }

        scm {
          connection = 'scm:git:' + project.ext.vcsAddress
          developerConnection = project.ext.vcsAddress
          url = websiteAddress
        }
      }
    }
  }
}

spotless {
  java {
    lineEndings 'UNIX'
    endWithNewline()
    googleJavaFormat('1.20.0')
  }

  groovy {
    indentWithSpaces(4)
    importOrder()

    excludeJava()
    greclipse("4.26").configFile rootProject.files(
        'gradle/spotless/spotless.eclipseformat.xml',
        'gradle/spotless/spotless.groovyformat.prefs')
  }

  groovyGradle {
    target '*.gradle', 'gradle/**/*.gradle'

    greclipse("4.26").configFile rootProject.files(
        'gradle/spotless/spotless.eclipseformat.xml',
        'gradle/spotless/spotless.groovyformat.prefs')
  }
}

task tidy() {
  description "Applies formatters and cleanups to sources (if configured)."
  group "verification"

  dependsOn tasks.matching { task -> task.name == "spotlessApply" }
}

check.dependsOn tasks.matching { task -> task.name == "spotlessCheck" }
